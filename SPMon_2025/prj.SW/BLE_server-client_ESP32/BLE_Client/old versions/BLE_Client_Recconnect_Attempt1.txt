/*
 * BLE Client test ESP32 - Receive a dummy payload from the server and print it on client side
 */

#include "BLEDevice.h"

/* Expected format and size of the payload */
typedef struct __attribute__((packed)) {
    uint16_t cnt;
    float tempDHT;
    float humDHT;
    float tempDS18B20;
    float tempTHR;
    uint16_t lux;
    uint16_t spec[11];
} SensorPayload;

SensorPayload payload;

/* BLE Server name (the other ESP32 name running the server sketch) */
#define BLE_SERVER_NAME "BLE_SERVER_ESP32"

/* UUID's of the service, characteristic that we want to read*/
static BLEUUID serverServiceUUID("91bad492-b950-4226-aa2b-4ede9fa42f59");

/* Characteristic */
static BLEUUID testCharacteristicUUID("cba1d466-344c-4be3-ab3f-189f80dd7518");

/* Flags stating if should begin connecting and if the connection is up */
static boolean doConnect = false;
static boolean connected = false;

/* Address of the peripheral device. Address will be found during scanning */
static BLEAddress* pServerAddress;

/* Characteristics that we want to read */
static BLERemoteCharacteristic* testCharacteristic;

/* Activate notify */
const uint8_t notificationOn[] = { 0x1, 0x0 };
const uint8_t notificationOff[] = { 0x0, 0x0 };

/* Connect to the BLE Server that has the name, Service, and Characteristics */
bool connectToServer(BLEAddress pAddress) {
  BLEClient* pClient = BLEDevice::createClient();

  /* Connect to the remote BLE Server */
  pClient->connect(pAddress);
  /* Set maximum packet size */
  pClient->setMTU(517);
  Serial.println(">>> Connected to server");

  /*Obtain a reference to the service we are after in the remote BLE server */
  BLERemoteService* pRemoteService = pClient->getService(serverServiceUUID);
  if (pRemoteService == nullptr) {
    Serial.print(">>> Failed to find our service UUID: ");
    Serial.println(serverServiceUUID.toString().c_str());
    return (false);
  }

  /* Obtain a reference to the characteristics in the service of the remote BLE server */
  testCharacteristic = pRemoteService->getCharacteristic(testCharacteristicUUID);
  if (testCharacteristic == nullptr) {
    Serial.print(">>> Failed to find our characteristic UUID");
    return false;
  }
  Serial.println(">>> Found our characteristics");

  /* Assign callback functions for the Characteristics */
  testCharacteristic->registerForNotify(testNotifyCallback);
  return true;
}

/* Callback function that gets called, when another device's advertisement has been received */
class MyAdvertisedDeviceCallbacks : public BLEAdvertisedDeviceCallbacks {
  void onResult(BLEAdvertisedDevice advertisedDevice) {
    /* Check if the name of the advertiser matches */
    if (advertisedDevice.getName() == BLE_SERVER_NAME) {
      /* Scan can be stopped, we found what we are looking for */
      advertisedDevice.getScan()->stop();

      /* Address of advertiser is the one we need */
      pServerAddress = new BLEAddress(advertisedDevice.getAddress());

      /* Set indicator, stating that we are ready to connect */
      doConnect = true;
      Serial.println(">>> Device found. Connecting!");
    }
  }
};

/* When the BLE Server sends a new reading with the notify property */
static void testNotifyCallback( BLERemoteCharacteristic* pBLERemoteCharacteristic,
                                uint8_t* pData,
                                size_t length,
                                bool isNotify) 
{
  if (length == sizeof(SensorPayload)) 
  {
      memcpy(&payload, pData, sizeof(SensorPayload));

      Serial.println(">>> PAYLOAD RECEIVED:");
      Serial.printf("CNT: %d | DHT_T: %.2f | DHT_H: %.2f | DS18B20: %.2f | THR: %.2f | LUX: %d\n",
                    payload.cnt, 
                    payload.tempDHT, 
                    payload.humDHT, 
                    payload.tempDS18B20,
                    payload.tempTHR, 
                    payload.lux);
      Serial.print("Spectre: ");
      for (int i = 0; i < 11; i++) 
      {
          Serial.print(payload.spec[i]);
          if (i < 10) Serial.print(",");
      }
      Serial.println();
      Serial.print(">>> Packet length: "); Serial.println(length);

  } 
  else 
  {
      Serial.println(">>> Wrong packet size!");
      Serial.println(length);
  }
}

void setup() 
{
    Serial.begin(115200);
    Serial.println(">>> Starting Arduino BLE Client application...");

    /* Init BLE device */
    BLEDevice::init("");
    BLEDevice::setMTU(517);

    /* 
      * Retrieve a Scanner and set the callback we want to use to be informed when we
      * have detected a new device.  Specify that we want active scanning and start the
      * scan to run for 30 seconds. 
      */
    BLEScan* pBLEScan = BLEDevice::getScan();
    pBLEScan->setAdvertisedDeviceCallbacks(new MyAdvertisedDeviceCallbacks());
    pBLEScan->setActiveScan(true);
    pBLEScan->start(30);
}

void loop() 
{
    /*
      * If the flag "doConnect" is true then we have scanned for and found the desired
      * BLE Server with which we wish to connect.  Now we connect to it.  Once we are
      * connected we set the connected flag to be true.
      */
    if (doConnect == true) 
    {
        if (connectToServer(*pServerAddress)) 
        {
            Serial.println(">>> We are now connected to the BLE Server.");
            /* Activate Notify property for the Characterisitc */
            testCharacteristic->getDescriptor(BLEUUID((uint16_t) 0x2902))->writeValue((uint8_t*)notificationOn, 2, true);
            connected = true;
        } 
        else 
        {

        }
        doConnect = false;
    }
    else
    {
          Serial.println(">>> We have failed to connect to the server; Restart your device to scan for nearby BLE server again.");
          connected = false;
    }

    if (!connected) 
    {
        Serial.println(">>> Not connected. Scanning for server...");
        BLEScan* pBLEScan = BLEDevice::getScan();
        pBLEScan->setAdvertisedDeviceCallbacks(new MyAdvertisedDeviceCallbacks());
        pBLEScan->setActiveScan(true);
        pBLEScan->start(5); 
        delay(5000);         
    }

    delay(1000);
}
